;
; Copyright (c) 2018 Anthony Beaucamp.
;
; This software is provided 'as-is', without any express or implied warranty.
; In no event will the authors be held liable for any damages arising from
; the use of this software.
;
; Permission is granted to anyone to use this software for any purpose,
; including commercial applications, and to alter it and redistribute it
; freely, subject to the following restrictions:
;
;   1. The origin of this software must not be misrepresented; you must not
;   claim that you wrote the original software. If you use this software in a
;   product, an acknowledgment in the product documentation would be
;   appreciated but is not required.
;
;   2. Altered source versions must be plainly marked as such, and must not
;   be misrepresented as being the original software.
;
;   3. This notice may not be removed or altered from any distribution.
;
;   4. The names of this software and/or it's copyright holders may not be
;   used to endorse or promote products derived from this software without
;   specific prior written permission.
;

; Twin frame graphic mode for Atari
; Adapted from Inter-Player
; Date created: 2017/12/21
; Compile with MADS

lists	= $6520
hbuf1   = $6525
hbuf2   = $658d
procs   = $6f50

bmp1	= $7010
bmp2	= $a010

atract  = $004d
vdslst	= $0200
sdlstl	= $0230

color0	= $02c4
color1	= $02c5
color2	= $02c6
color3	= $02c7
color4	= $02c8

M1POS 	= $d004
M2POS 	= $d005
M3POS 	= $d006
M4POS 	= $d007

gprior	= $d01b
wsync	= $d40a
nmien	= $d40e

/*-------------------------------------------------------------------------------------------------*/
	
	org lists

; Display lists	
dlist:	dta $70,$70,$70
		dta $4e,a(bmp1)
		:101 dta $e			
		dta $4e,0,h(bmp1+$1000)
		:96 dta $e
		dta $8e				; Invoke DLI with $80
		dta $41,a(dlist)
		
/*-------------------------------------------------------------------------------------------------*/	
		
// 5th sprite flicker routine

initPM5:
	; Init 5th sprite
	mva #0 PM5IND
	mva #0 PMGMask
	rts

updtPM5:	
	; Check that sprites are enabled!
	lda PMGMask
	bne sprbranch0
	rts
	
sprbranch0:
	; Reset pixels of 5th sprite
	ldx PM5AOU+0	; Address of 5th sprite
	stx $80
	ldx PM5AOU+1
	stx $81	
	ldy #13			; Sprite length
	lda #$00		; Reset value
strloop0:	
	sta ($80),y		
	dey				
	bne strloop0	; Loop through till y is 0
	
sprbranch1:
	; Should we increment sprite index? (depends on palette toggle)
	;lda palTOG
	;beq sprbranch3
	
	; Increment 5th sprite index (reset when index becomes 4)
	ldx PM5IND
	inx	
	cpx #4
	bcc sprbranch2
	ldx #0
	
sprbranch2:
	stx PM5IND
	
	; Verify sprite is allocated in mask
	lda #$0000001
	cpx #0
	beq bittest
bitshift:
	asl
	dex
	bne bitshift	; Loop through till x is 0 
bittest:
	and PMGMask
	beq sprbranch1
	
sprbranch3:
	; Set x pos. of new 5th sprite
	ldx PM5IND
	lda PMGX,x  ; Xpos of sprite to A
	sta M4POS
	cpx #0
	adc #1
	sta M3POS
	adc #2
	sta M2POS
	adc #2
	sta M1POS
	
	; Offset of IN address (frame of 5th sprite)
	cpx #0
	beq off0
	cpx #1
	beq off1
	cpx #2
	beq off2
off3:
	mva PMGFram+7 PM5AIN+1
	mva PMGFram+6 PM5AIN+0
	jmp add0
off2:	
	mva PMGFram+5 PM5AIN+1
	mva PMGFram+4 PM5AIN+0
	jmp add0
off1:	
	mva PMGFram+3 PM5AIN+1
	mva PMGFram+2 PM5AIN+0
	jmp add0
off0:	
	mva PMGFram+1 PM5AIN+1
	mva PMGFram+0 PM5AIN+0
		
	; Add base of IN address ($98A0)
add0:
	lda #$a0
	clc
	adc PM5AIN
	sta PM5AIN
	lda #$98
	adc PM5AIN+1
	sta PM5AIN+1
	
	; Offset of OUT address (ypos of 5th sprite)
	lda PMGY,x   ; Ypos of sprite to A
	sta PM5AOU
	lda #0 
	sta PM5AOU+1

	; Add base of OUT address ($9b00)
	lda #$00
	clc
	adc PM5AOU
	sta PM5AOU
	lda #$9b
	adc PM5AOU+1
	sta PM5AOU+1

	; Set Address for IN
	ldx PM5AIN+0
	stx $0
	ldx PM5AIN+1
	stx $1

	; Set Address for OUT
	ldx PM5AOU+0
	stx $2
	ldx PM5AOU+1
	stx $3
		
	; Set pixels of 5th sprite
	ldy #12	 ; Sprite length
sprloop1:
	; Transfer values IN/OUT
	lda ($0),y	
	sta ($2),y	
	dey
	bne sprloop1	; Loop through till y is 0 
	
return:	
	; All done!
	rts

/*-------------------------------------------------------------------------------------------------*/

	org procs
	
; Palette toggle
palTOG:	dta $0

; 5th sprite flicker
PMGMask	.ds 1
PMGX	.ds 4
PMGY	.ds 4
PMGFram	.ds 8
PM5IND	.ds 1
PM5AIN	.ds 2
PM5AOU	.ds 2

; GFX config backup
nmienT  .ds 1
dlistT  .ds 2

; Registries backup
regA	.ds 1
regX	.ds 1
regY	.ds 1	
		
.proc StartRAW	
	; Backup GFX settings
	mva nmien   nmienT
	mwa sdlstl  dlistT
	
	; Setup DLIST and DLI
	sei
	mva	#$c0	nmien
	mwa	#dlist	sdlstl
	mwa	#dli	vdslst
	cli

	; Init 5th sprite
	jsr initPM5
	rts		
.endp

.proc StopRAW
	; Kill DLI
	sei
	mva	nmienT	nmien
	mwa dlistT	sdlstl
	cli
	rts
.endp

dli:
	; Backup registers
	sta regA
	stx regX
	sty regY
	
	; Switch palette
	lda palTOG
	eor #$1
	sta palTOG
	bne frame2

frame1:	
	; Switch bitmap buffer 1
	mva #$A0 hbuf1
	mva #$B0 hbuf2	
	jmp done

frame2:	
	; Switch bitmap buffer 2
	mva #$70 hbuf1
	mva #$80 hbuf2		
	jmp done
	
done:
	; Flicker 5th sprite (if needed)
	jsr updtPM5

	; Reset atract (screen saver timer)
	sta atract

	; Restore Registers
	lda regA
	ldx regX
	ldy regY
	rti	
	