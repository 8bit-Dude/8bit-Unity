// Twin Frame RAW graphics mode for Atari
// Adapted from XL-Paint, DLIs were removed to speed-up display
// Compile with MADS

procs   = $8E10
data	= $BE10

palette	= $7000
buf0	= $7010
buf1	= $A010

atract  = $004d
vdslst	= $0200
sdlstl	= $0230

color0	= $02c4
color1	= $02c5
color2	= $02c6
color3	= $02c7
color4	= $02c8

gprior	= $d01b
wsync	= $d40a
nmien	= $d40e

initPMG5 = $8ef7
updtPMG5 = $8efd

/*-------------------------------------------------------------------------------------------------*/
	
	org data

	;.align	$100
	
dlist0:	dta d'pp',$30+$80
		dta $4e,a(buf0)
		:101 dta $e
		dta $4e,0,h(buf0+$1000)
		:88 dta $e
		dta $e
		dta $41,a(dlist0)

	;.align	$100	
		
dlist1:	dta d'pp',$30+$80
		dta $4e,a(buf1)
		:101 dta $e
		dta $4e,0,h(buf1+$1000)
		:88 dta $e
		dta $e
		dta $41,a(dlist1)	
	
; Palette switcher
palS:	dta $0

; GFX config backup
nmienT  .ds 1
dlistT  .ds 2
dliT	.ds 2

; Registries backup
regA	.ds 1
regX	.ds 1
regY	.ds 1

/*-------------------------------------------------------------------------------------------------*/

	org procs
	
.proc StartRAW	
	; Backup GFX settings
	mva nmien   nmienT
	mwa sdlstl  dlistT
	mwa vdslst  dliT

	; Setup DLIST and DLI
	sei
	mva	#$c0	nmien
	mwa	#dlist0	sdlstl
	mwa	#dli	vdslst
	cli

	; Init 5th sprite
	jsr initPMG5
	rts		
.endp

/*-------------------------------------------------------------------------------------------------*/

.proc StopRAW
	; Kill DLI
	sei
	mva	nmienT	nmien
	mwa dlistT	sdlstl
	mwa dliT	vdslst 
	cli
	
	; Black-out palette
	;mva #0 color4
	;mva #0 color0
	;mva #0	color1
	;mva #0	color2	
		
	rts
.endp
	
/*-------------------------------------------------------------------------------------------------*/

dli:
	; Backup registers
	sta regA
	stx regX
	sty regY
	
	; Switch palette
	lda palS
	eor #$1
	sta palS
	bne pal1

pal0:	
	; Assign palette 1
	mva palette+4	color4
	mva palette+5	color0
	mva palette+6	color1
	mva palette+7	color2

	; Switch Display List
	mwa	#dlist1		sdlstl
	jmp done

pal1:	
	; Assign palette 0
	mva palette+0	color4
	mva palette+1	color0
	mva palette+2	color1
	mva palette+3	color2

	; Switch Display List
	mwa	#dlist0		sdlstl
	jmp done
	
done:
	; Flicker 5th sprite? (check paletter toggle)
	lda palS
	jsr updtPMG5

	; Reset atract (screen saver timer)
	sta atract

	; Restore Registers
	lda regA
	ldx regX
	ldy regY
	rti
